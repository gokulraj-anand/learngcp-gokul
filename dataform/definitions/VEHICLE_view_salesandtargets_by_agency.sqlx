config {
    type: "view",
    name: "vw_salesandtargets_by_agency",
    description: "View with details of sales numbers and targets by agency. This contains agency deatail even if there are no sales but has a target",
    columns: {
        agency_id: "ID Number of the agency",
        agency_name: "Name of the agency",
        agency_city: "City the agency is located in",
        sale_month: "Month of Sales/Targets",
        sales: "Vehicles Sold",
        targets: "Target for Sale"
    },
    tags: "inventory_twowheelers"
}

--Existing Actual returns all records with Sales for Month/Agency
WITH
  Existing_Actual AS (
  SELECT
    DISTINCT SbA.agency_id,
    SbA.sale_month,
  FROM
    ${ref("tbl_sales_by_agency")} SbA ),

--Insert Actual returns all records without Sales for Month/Agency
  Insert_Actual AS (
  SELECT
    DISTINCT TA.agency_id,
    TA.sale_month
  FROM
    ${ref("tbl_targets_by_agency")} TA
  LEFT JOIN
    Existing_Actual EA
  ON
    EA.agency_id = TA.agency_id
    AND EA.sale_month = TA.sale_month
  WHERE
    EA.agency_id IS NULL )

--Final Query to Insert Records into the Main Table using an union all with one select for values with actual sales and one for month/agency without actual sales
SELECT
  SA.agency_id,
  SA.agency_name,
  SA.agency_city,
  SbA.sale_month,
  SbA.sales,
  TA.targets
FROM
  ${ref("bqdataset", "tbl_sales_agency")} SA
LEFT JOIN
  ${ref("tbl_sales_by_agency")} SbA
ON
  SA.agency_id = SbA.agency_id
LEFT JOIN
  ${ref("tbl_targets_by_agency")} TA
ON
  SbA.agency_id = TA.agency_id
  AND SbA.sale_month = TA.sale_month
INNER JOIN
  Existing_Actual EA
ON
  SbA.agency_id = EA.agency_id
  AND SbA.sale_month = EA.sale_month

UNION ALL

SELECT
  SA.agency_id,
  SA.agency_name,
  SA.agency_city,
  TA.sale_month,
  NULL AS sales,
  TA.targets
FROM
  ${ref("bqdataset", "tbl_sales_agency")} SA
LEFT JOIN
  ${ref("tbl_targets_by_agency")} TA
ON
  SA.agency_id = TA.agency_id
INNER JOIN
  Insert_Actual IA
ON
  TA.agency_id = IA.agency_id
  AND TA.sale_month = IA.sale_month
